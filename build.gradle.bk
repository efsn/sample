buildscript {

    ext {
        springBootVersion = "2.1.3.RELEASE"
        springCloudVersion = "Greenwich.SR2"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        /*
        maven {
            url("https://plugins.gradle.org/m2/")
        }
        */
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }

}

apply from: "build-dependencies.gradle"

subprojects {

    apply plugin: "idea"
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"


    group = "cn.elmi.sample"
    version = "1.0-SNAPSHOT"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava, compileTestJava, javadoc]*.options*.encoding = "UTF-8"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    configurations {
        all*.exclude module: ["commons-logging", "commons-lang", "guava"]
    }

    // implements maven import pom
    dependencyManagement {
        imports {
            mavenBom lib.spring.boot.dependencies
            mavenBom lib.spring.cloud.dependencies
        }
    }

    jar {
        enabled = true
    }

    bootJar {
        manifest {
            attributes "Start-Class": "cn.elmi.sample.ClientApplication"
        }
    }

    dependencies {
        compile lib.spring.boot.starter.core,
                lib.slf4j,
                lib.logback.classic,
                lib.logback.core

        testCompile lib.testng, lib.spring.boot.starter.test

        compileOnly lib.lombok
        annotationProcessor lib.lombok
    }

}

